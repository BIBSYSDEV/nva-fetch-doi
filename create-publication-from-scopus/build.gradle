def problemVersion = '0.24.0'

project.ext {
    jaxbTargetDir = file("src/main/java")
}

configurations {
    jaxb
}

//compileJava.dependsOn jaxb

dependencies {
    implementation project(":fetch-doi-commons")
    implementation project(":doi-transformer") {
        compileOnly(libs.jakarta) {
            because 'PMD'
        }
    }
    implementation project(":doi-proxy")
    implementation project(":metadata-transformer")
    implementation libs.aws.java.sdk.s3
    jaxb libs.bundles.xsd2java

    testImplementation(libs.zalando.problem) {
        compileOnly(libs.apiGuardian) {
            because 'Dependency has maven scope provided'
        }
    }
}

task jaxb() {
    doLast {
        jaxbTargetDir.mkdirs()
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.jaxbTargetDir = jaxbTargetDir
        ant.xjc(
                destdir: '${jaxbTargetDir}',
                package: 'no.scopus.generated',
                schema: 'src/main/resources/schema.scopus.xocs-ani514/xocs-ani514.xsd',
                binding: 'src/main/resources/schema.scopus.xocs-ani514/xocs-ani514.xjb'
        )
    }
}

tasks.withType(Checkstyle) {
    exclude { 'no/scopus/generated/*' }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'no/scopus/generated/*'
            ])
        })
    }
}