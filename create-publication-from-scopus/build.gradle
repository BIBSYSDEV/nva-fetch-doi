def problemVersion = '0.24.0'

project.ext {
    jaxbTargetDir = file("$buildDir/generated/sources/xjc")
}

sourceSets.main.java.srcDirs += jaxbTargetDir

configurations {
    jaxb
}

dependencies {
    //TODO: sjekke om vi trenger alle disse sub-modules inn her
    implementation project(":fetch-doi-commons")
    implementation project(":doi-transformer") {
        compileOnly(libs.jakarta) {
            because 'PMD'
        }
    }
    jaxb libs.bundles.xsd2java
    implementation libs.bundles.xsd2java
    implementation project(":doi-proxy")
    implementation project(":metadata-transformer")
    implementation libs.aws.java.sdk.s3

    testImplementation(libs.zalando.problem) {
        compileOnly(libs.apiGuardian) {
            because 'Dependency has maven scope provided'
        }
    }
}

task xsd2java() {
    doLast {
        jaxbTargetDir.mkdirs()
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.jaxbTargetDir = jaxbTargetDir
        ant.xjc(
                destdir: '${jaxbTargetDir}',
                package: 'no.scopus.generated',
                schema: 'src/main/resources/schema.scopus.xocs-ani515/xocs-ani515.xsd',
                binding: 'src/main/resources/schema.scopus.xocs-ani515/xocs-ani515.xjb'
        )
    }
}

tasks.withType(JavaCompile) {
    dependsOn(xsd2java)
}
