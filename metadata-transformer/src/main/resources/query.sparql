PREFIX any23: <http://vocab.sindice.net/any23#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX unit: <https://nva.unit.no/publication#>

CONSTRUCT {
  <__URI__> a unit:PublicationRequest ;
    unit:entityDescription ?entityNode .
  ?entityNode a unit:EntityDescription ;
    unit:mainTitle ?mainTitle ;
    unit:description ?description ;
    unit:abstract ?abstract ;
    unit:tag ?tag ;
    unit:contributor ?contributorNode ;
    unit:date ?dateNode .
  ?contributorNode a unit:Contributor ;
    unit:identity ?identityNode .
  ?identityNode a unit:Identity ;
    unit:name ?name .
  ?dateNode a unit:PublicationDate ;
    unit:year ?year ;
    unit:month ?month ;
    unit:day ?day .
} WHERE {
  SELECT DISTINCT (STR(?title) AS ?mainTitle)
                  ?entityNode
                  ?contributorNode
                  ?identityNode
                  (STR(?creator) AS ?name)
                  (STR(?subject) AS ?tag)
                  (STR(?rawDate) AS ?date)
                  ?description
                  ?abstract
                  (STR(?rawLanguage) AS ?language)
                  ?dateNode
                  ?year
                  ?month
                  ?day {

    BIND (BNODE() AS ?entityNode)
    {
      SELECT ?title WHERE {
        VALUES ?titleProperty { any23:DC.title any23:citation_title }
        <__URI__> ?titleProperty ?title  .
      } ORDER BY DESC(STRLEN(STR(?title))) LIMIT 1
    } UNION {
      <__URI__> any23:DC.creator | any23:DC.contributor ?creator .

      BIND(BNODE() AS ?contributorNode)
      BIND(BNODE() AS ?identityNode)
      FILTER (BOUND(?creator) && STRLEN(?creator)>0)
    }
    UNION {
      <__URI__> any23:DC.subject | any23:DC.coverage ?subject .
    }
    UNION {
      <__URI__> any23:DC.date ?rawDate .

      BIND(BNODE() AS ?dateNode)
      BIND(STR(?rawDate) AS ?stringDate)
      BIND(SUBSTR(?stringDate, 0, 5) AS ?year)
      BIND(IF(STRLEN(?stringDate) > 6, SUBSTR(?stringDate, 6, 2), "") AS ?rawMonth)
      BIND(IF(STRLEN(?stringDate) > 8, SUBSTR(?stringDate, 9, 2), "") AS ?rawDay)
      OPTIONAL {
        BIND(?rawMonth AS ?month)
        FILTER(?rawMonth != "")
      }
      OPTIONAL {
        BIND(?rawDay AS ?day)
        FILTER(?rawDay != "")
      }
      FILTER (BOUND(?stringDate) && REGEX(?stringDate, "^[0-9]{4}(-[0-9]{2}(-[0-9]{2})?)?$"))
    }
    UNION {
      SELECT DISTINCT ?abstract ?description WHERE {
        {
          <__URI__> any23:DC.description ?rawDescription .
        } UNION {
          <__URI__> any23:DCTERMS.abstract ?rawAbstract .
        }

        BIND(STR(?rawAbstract) AS ?abstract)

        OPTIONAL {
          BIND (STR(?rawDescription) AS ?abstract)
          FILTER NOT EXISTS {
            <__URI__> any23:DCTERMS.abstract ?rawAbstract
          }
        }
        OPTIONAL {
          BIND(STR(?rawDescription) AS ?description)
          FILTER EXISTS {
            <__URI__> any23:DCTERMS.abstract ?rawAbstract
          }
        }
      }
    }
    UNION {
        <__URI__> any23:DC.language ?rawLanguage .
    }
  }
}