PREFIX any23: <http://vocab.sindice.net/any23#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX unit: <https://unit.no/ontology#>

CONSTRUCT {
  <__URI__> a unit:PublicationRequest ;
    unit:entityDescription ?entityNode .
  ?entityNode a unit:EntityDescription ;
    unit:mainTitle ?title ;
    unit:description ?description ;
    unit:tag ?subject ;
    unit:contributor ?contributorNode .
  ?contributorNode a unit:Contributor ;
    unit:identity ?identityNode .
  ?identityNode a unit:Identity ;
    unit:name ?creator .
} WHERE {
  select DISTINCT ?title ?entityNode ?contributorNode ?identityNode ?creator ?subject ?date ?description ?language {
    BIND (BNODE() AS ?entityNode)

    {
      <__URI__> any23:dc.title ?title
    }
    UNION {
      <__URI__> any23:dc.creator ?creator
      BIND(BNODE() AS ?contributorNode)
      BIND(BNODE() AS ?identityNode)
      FILTER (BOUND(?creator) && strlen(?creator)>0)
    }
    UNION {
      <__URI__> any23:dc.subject ?subject
    }
    UNION {
      <__URI__> any23:dc.date ?date
    }
    UNION {
      <__URI__> any23:dc.description ?description
    }
    UNION {
      <__URI__> any23:dc.language ?language
    }
  }
}